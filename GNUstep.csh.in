#!/bin/csh
#
#   GNUstep.csh.in
#
#   Shell initialization for the GNUstep environment.
#
#   Copyright (C) 1998 Free Software Foundation, Inc.
#
#   Author:  Scott Christley <scottc@net-community.com>
#
#   This file is part of the GNUstep Makefile Package.
#
#   This library is free software; you can redistribute it and/or
#   modify it under the terms of the GNU General Public License
#   as published by the Free Software Foundation; either version 2
#   of the License, or (at your option) any later version.
#   
#   You should have received a copy of the GNU General Public
#   License along with this library; see the file COPYING.LIB.
#   If not, write to the Free Software Foundation,
#   59 Temple Place - Suite 330, Boston, MA 02111-1307, USA.
#

#
# Set the GNUstep system root and local root
#
setenv GNUSTEP_ROOT @GNUSTEP_ROOT@
setenv GNUSTEP_SYSTEM_ROOT @prefix@
setenv GNUSTEP_FLATTENED @GNUSTEP_FLATTENED@
if ( ! ${?LIBRARY_COMBO} ) then
  setenv LIBRARY_COMBO @ac_cv_library_combo@
endif

setenv GNUSTEP_LOCAL_ROOT @GNUSTEP_LOCAL_ROOT@
setenv GNUSTEP_NETWORK_ROOT @GNUSTEP_NETWORK_ROOT@
setenv GNUSTEP_USER_ROOT ~/GNUstep

setenv GNUSTEP_MAKEFILES ${GNUSTEP_SYSTEM_ROOT}/Makefiles

#
# Determine the host information
#
if ( ! ${?GNUSTEP_HOST} ) then
  pushd /tmp > /dev/null
  setenv GNUSTEP_HOST `${GNUSTEP_MAKEFILES}/config.guess`
  setenv GNUSTEP_HOST `${GNUSTEP_MAKEFILES}/config.sub ${GNUSTEP_HOST}`
  popd > /dev/null
endif

if ( ! ${?GNUSTEP_HOST_CPU} ) then
  setenv GNUSTEP_HOST_CPU `${GNUSTEP_MAKEFILES}/cpu.sh ${GNUSTEP_HOST}`
  setenv GNUSTEP_HOST_CPU `${GNUSTEP_MAKEFILES}/clean_cpu.sh ${GNUSTEP_HOST_CPU}`
endif

if ( ! ${?GNUSTEP_HOST_VENDOR} ) then
  setenv GNUSTEP_HOST_VENDOR `${GNUSTEP_MAKEFILES}/vendor.sh ${GNUSTEP_HOST}`
  setenv GNUSTEP_HOST_VENDOR `${GNUSTEP_MAKEFILES}/clean_vendor.sh ${GNUSTEP_HOST_VENDOR}`
endif

if ( ! ${?GNUSTEP_HOST_OS} ) then
  setenv GNUSTEP_HOST_OS `${GNUSTEP_MAKEFILES}/os.sh ${GNUSTEP_HOST}`
  setenv GNUSTEP_HOST_OS `${GNUSTEP_MAKEFILES}/clean_os.sh ${GNUSTEP_HOST_OS}`
endif

#
# Add the GNUstep tools directories to the path
#
if ( ! ${?GNUSTEP_PATHPREFIX} ) then
  setenv GNUSTEP_PATHPREFIX \
         ${GNUSTEP_USER_ROOT}:${GNUSTEP_LOCAL_ROOT}:${GNUSTEP_NETWORK_ROOT}:${GNUSTEP_SYSTEM_ROOT}
endif

set tp = ""
foreach dir ( `/bin/sh -c 'IFS=:; for i in ${GNUSTEP_PATHPREFIX}; do echo $i; done'` )
  if ( ! ${?GNUSTEP_FLATTENED} ) then
    set tp=${tp}${dir}/Tools/${GNUSTEP_HOST_CPU}/${GNUSTEP_HOST_OS}:
  endif
  set tp=${tp}${dir}/Tools:
end
setenv GNUSTEP_PATHPREFIX_LIST $GNUSTEP_PATHPREFIX
setenv PATH ${tp}${PATH}

source ${GNUSTEP_SYSTEM_ROOT}/Makefiles/ld_lib_path.csh

#
# Perform any user initialization
#
if ( -e ~/.GNUstep/GNUstep.csh ) then
  . ~/.GNUstep/GNUstep.csh
endif

if ( ${?GNUSTEP_FLATTENED} ) then
  set TDIR=${GNUSTEP_SYSTEM_ROOT}/Tools/${GNUSTEP_HOST_CPU}/${GNUSTEP_HOST_OS}
  if ( -e $TDIR/$LIBRARY_COMBO/make_services ) then
    $TDIR/$LIBRARY_COMBO/make_services &
    if ( -e $TDIR/make_services ) then
      $TDIR/make_services &
    endif
  endif
else
  set TDIR=${GNUSTEP_SYSTEM_ROOT}/Tools
  if ( -e $TDIR/make_services ) then
    $TDIR/make_services &
  endif
endif

#
#
#
set gnustep_class_path="${GNUSTEP_USER_ROOT}/Libraries/Java:${GNUSTEP_LOCAL_ROOT}/Libraries/Java:${GNUSTEP_NETWORK_ROOT}/Libraries/Java:${GNUSTEP_SYSTEM_ROOT}/Libraries/Java"

if ( ! ${?CLASSPATH} ) then
  setenv CLASSPATH "${gnustep_class_path}"
else
  setenv CLASSPATH "${CLASSPATH}:${gnustep_class_path}"
endif

#
#
